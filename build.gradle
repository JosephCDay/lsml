buildscript {
	repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
  	}
}

plugins {
	id "application"
	id "java"
	id "findbugs"
	id "jacoco"
	id "eclipse"
	id "edu.sc.seis.launch4j" version "2.4.2"
	id "net.ltgt.apt" version "0.13"
    id "com.dorongold.task-tree" version "1.3"
}

repositories {
	jcenter()
}

allprojects {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

/* ----------------------------------------------------------------------------
 * General project configuration
 * --------------------------------------------------------------------------*/
def installerDir = project.projectDir.toString() + '/installer/';
applicationName = 'Li Song Mechlab'
archivesBaseName = 'lsml'
mainClassName = 'org.lisoft.lsml.view_fx.LiSongMechLab'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

/* ----------------------------------------------------------------------------
 * Only use Binary Style Sheets (BSS)
 * --------------------------------------------------------------------------*/
sourceSets{
	main{
		resources {
			exclude '**/*.css'
		}
	}
}

task compileCss(/*type: Exec*/){
/*	executable 'javapackager'
	args '-createbss', '-srcdir', 'src/main/resources/view/', '-outdir', 'src/main/resources/view/'*/
    print "BSS compilation disabled until javapackager is fixed for Java 9."
}

compileJava.dependsOn compileCss

/* ----------------------------------------------------------------------------
 * Dependencies
 * --------------------------------------------------------------------------*/


dependencies {
	compile 'com.thoughtworks.xstream:xstream:1.4.10+',
			'net.java.dev.jna:jna:4.4.0+',
			"com.google.dagger:dagger:2.13"
			
  	apt     "com.google.dagger:dagger-compiler:2.13"

	testCompile 'junit:junit:4.12+', 
				'org.mockito:mockito-core:2.2.8+', 
				'pl.pragmatists:JUnitParams:1.1.0+'
}

/* ----------------------------------------------------------------------------
 * Tests, Verification and Reports
 * --------------------------------------------------------------------------*/
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

findbugs {
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/view_fx/**',
                              '**/database/gamedata/**',
                              '**/database/gamedata/MwoDataReader**',
                              '**/view_headless/**'])
        })
    }
}
check.dependsOn jacocoTestReport

/* ----------------------------------------------------------------------------
 * Release Management
 * --------------------------------------------------------------------------*/
createExe{
	jreMinVersion = "1.8.0_77"
	mainClassName = project.mainClassName
	dontWrapJar = true
	outputDir = installerDir
	icon = installerDir + '/icon.ico'
	jar = 'lsml.jar'
	manifest = installerDir + '/lsml.manifest'
}

task wixCandle64(type: Exec) {
	workingDir 'installer'
	executable 'candle'
	args '-dg_64bit="yes"', '-dg_version=' + version, '"*.wxs"'
    dependsOn = ["createExe"]
}

task wixLight64(type: Exec) {
	workingDir 'installer'
	executable 'light'
	args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_64bit.msi'
    dependsOn = ["wixCandle64"]
}

task wixCandle32(type: Exec) {
	workingDir 'installer'
	executable 'candle'
	args '-dg_64bit="no"', '-dg_version=' + version, '"*.wxs"'
    dependsOn = ["createExe"]
}

task wixLight32(type: Exec) {
	workingDir 'installer'
	executable 'light'
	args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_32bit.msi'
    dependsOn = ["wixCandle32"]
}

gradle.taskGraph.whenReady { taskGraph ->
	if (!taskGraph.hasTask(release)) {
		version = '(develop)'
	}
}

task release {
    dependsOn = ["wixLight64", "wixLight32"]
}

jar {
	destinationDir = file(installerDir)
	from { 
		configurations.compile.collect { 
			it.isDirectory() ? it : zipTree(it) 
		} 
	}
	
	archiveName="lsml.jar"

	if(version == null){
		version = '0.0.0'
	}

	manifest {
		attributes(	'Main-Class': mainClassName, 
					'Implementation-Title': 'Li Song Mechlab', 
					'Implementation-Version' : version)
	}
}

jar.dependsOn check