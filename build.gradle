buildscript {
	repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
  	}
}

plugins {
	id "application"
	id "java"
	
	// Disable SpotBugs until they fix issue #500
	// https://github.com/spotbugs/spotbugs/issues/500
	//id "com.github.spotbugs" version "1.6.2"
	
	id "jacoco"
	id "edu.sc.seis.launch4j" version "2.4.4"
	id "net.ltgt.apt-eclipse" version "0.17"
    id "com.dorongold.task-tree" version "1.3"
}

repositories {
	jcenter()
}

allprojects {
	sourceCompatibility = 8.0
	targetCompatibility = 8.0
}

/* ----------------------------------------------------------------------------
 * General project configuration
 * --------------------------------------------------------------------------*/
applicationName = 'Li Song Mechlab'
archivesBaseName = 'lsml'
mainClassName = 'org.lisoft.lsml.view_fx.LiSongMechLab'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // These cause too much noise right now
            //options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

/* ----------------------------------------------------------------------------
 * Only use Binary Style Sheets (BSS)
 * --------------------------------------------------------------------------*/
sourceSets{
	main{
		resources {
			// BSS disabled for now, see below
			exclude '**/*.css'
		}
	}
}

task compileCss(type:Exec){
	executable 'javapackager'
	args '-createbss', '-srcdir', 'src/main/resources/view/', '-outdir', 'src/main/resources/view/'
    //println "BSS compilation disabled until javapackager is fixed for Java 9."
    // Remember to uncommet in sourceSets above!
}

compileJava.dependsOn compileCss

/* ----------------------------------------------------------------------------
 * Dependencies
 * --------------------------------------------------------------------------*/


dependencies {
	compile 'com.thoughtworks.xstream:xstream:1.4.10',
			'net.java.dev.jna:jna:4.5.1+',
			"com.google.dagger:dagger:2.16"
			
  	annotationProcessor "com.google.dagger:dagger-compiler:2.16"

	testCompile 'junit:junit:4.12+', 
				'org.mockito:mockito-core:2.19.0', 
				'pl.pragmatists:JUnitParams:1.1.1'
}

/* ----------------------------------------------------------------------------
 * Tests, Verification and Reports
 * --------------------------------------------------------------------------*/

/*
https://github.com/spotbugs/spotbugs/issues/500
spotbugsMain {
    reports {
        xml.enabled false
        html.enabled true
    }
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}

spotbugsTest {
    reports {
        xml.enabled false
        html.enabled true
    }
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}
*/

jacoco {
	// Fixes problems with jacoco and Java 10
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/view_fx/**',
                              '**/database/gamedata/**',
                              '**/database/gamedata/MwoDataReader**',
                              '**/view_headless/**'])
        })
    }
}
check.dependsOn jacocoTestReport

/* ----------------------------------------------------------------------------
 * Release Management
 * --------------------------------------------------------------------------*/
createExe{
	jreMinVersion = "1.8.171"
	mainClassName = project.mainClassName
	dontWrapJar = true
	outputDir = "../installer/"
	icon = '../installer/icon.ico'
	jar = 'lsml.jar'
	manifest = '../installer/lsml.manifest'
}

task wixCandle64(type: Exec) {
	workingDir 'installer'
	executable 'candle'
	args '-dg_64bit="yes"', '-dg_version=' + version, '"*.wxs"'
    dependsOn = ["createExe"]
}

task wixLight64(type: Exec) {
	workingDir 'installer'
	executable 'light'
	args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_64bit.msi'
    dependsOn = ["wixCandle64"]
}

task wixCandle32(type: Exec) {
	workingDir 'installer'
	executable 'candle'
	args '-dg_64bit="no"', '-dg_version=' + version, '"*.wxs"'
    dependsOn = ["createExe"]
}

task wixLight32(type: Exec) {
	workingDir 'installer'
	executable 'light'
	args '-ext', 'WixUIExtension', '"*.wixobj"', '-out', 'LSML_Setup-'+version+'_32bit.msi'
    dependsOn = ["wixCandle32"]
}

gradle.taskGraph.whenReady { taskGraph ->
	if (!taskGraph.hasTask(release)) {
		version = '(develop)'
	}
}

task release {
    dependsOn = ["wixLight64", "wixLight32"]
}

jar {
	destinationDir = file('installer/')
	from { 
		configurations.compile.collect { 
			it.isDirectory() ? it : zipTree(it) 
		} 
	}
	
	archiveName="lsml.jar"

	if(version == null){
		version = '0.0.0'
	}

	manifest {
		attributes(	'Main-Class': mainClassName, 
					'Implementation-Title': 'Li Song Mechlab', 
					'Implementation-Version' : version)
	}
}

jar.dependsOn check